= Basics

TypeQL is used for all queries to TypeDB.




All queries to a TypeDB database are written in TypeQL.

TypeQL is a *declarative* query language.
Rather than writing an algorithm of how the data should be retrieved, we declare requirements, and the TypeDB query
processor will take care of finding an optimal way to retrieve and process the data.

[NOTE]
====
TypeQL is both a https://en.wikipedia.org/wiki/Data_definition_language[Data Definition Language,window=_blank] (DDL)
and https://en.wikipedia.org/wiki/Data_manipulation_language[Data Manipulation Language,window=_blank] (DML).
====

As a DDL, it lets us define xref:typeql::schema/schema.adoc[schema] of a database.

As a DML, it lets us xref:typeql::queries/queries.adoc[query] a database.

The complete set of the TypeQL grammar can be seen in the *ANTLR v.4* specification file:
https://github.com/vaticle/typeql/blob/master/grammar/TypeQL.g4[TypeQL.g4,window=_blank].

== Query structure

A TypeQL xref:queries/queries.adoc[query] consists of clauses (one, two or three).
There are only six types of clauses:

* `define`
* `undefine`
* `match`
* `get`
* `insert`
* `delete`

A clause contains a single pattern, or a list of variables:

- The `get` clause starts with the `get` keyword, followed by a list of variables and optional modifiers at the end.
- All other TypeQL clauses consist of the defining keyword (the same as the clause's name) and a pattern.

A pattern consists of one or more statements.

.TypeQL query structure
* Query
** Clause
*** Pattern
**** Statement

.Simple query example
====
[,typeql]
----
match $p isa person;
get $p;
limit 1;
----

In the example above we can see a single xref:queries/get.adoc[Get] query with two clauses: `match` and `get`.
The query also has a modifier to xref:queries/get.adoc#_limit_the_results[limit] the number of results returned
to exactly one.
====

A string or a file with multiple TypeQL queries is a script.

.TypeQL script example
====
[,typeql]
----
insert
$p isa person,
    has full-name "Masako Holley",
    has email "masako.holley@vaticle.com";

insert
$p isa person,
    has full-name "Pearle Goodman",
    has email "pearle.goodman@vaticle.com";

insert
$p isa person,
    has full-name "Kevin Morrison",
    has email "kevin.morrison@vaticle.com";
----

The above example contains three xref:queries/insert.adoc[insert] queries without a match clause.

To rewrite it as a single query we need to remove the second and third `insert` keyword and change variables, so that
they are not overlapping (otherwise, we will insert a single person entity with three full-names and three emails):

[,typeql]
----
insert
$p1 isa person,
    has full-name "Masako Holley",
    has email "masako.holley@vaticle.com";

$p2 isa person,
    has full-name "Pearle Goodman",
    has email "pearle.goodman@vaticle.com";

$p3 isa person,
    has full-name "Kevin Morrison",
    has email "kevin.morrison@vaticle.com";
----
====

For more information on patterns, see the xref:queries/match.adoc[]
and the xref:queries/advanced.adoc[] pages.

== Formatting

=== Indentation

Any indentation in TypeQL will be ignored and serves are only for visual aid.

.Indentation example
====
The following three queries are identical in their result:

[,typeql]
----
insert $p isa person, has full-name "Masako Holley", has email "masako.holley@vaticle.com";

insert
$p isa person,
    has full-name "Masako Holley",
    has email "masako.holley@vaticle.com";

insert
$p
isa
person
,
has
full-name
"Masako Holley"
,
has
email
"masako.holley@vaticle.com"
;
----
====

//#todo TypeQL Styleguide

=== Comments

TypeQL uses in-line comments only. To make a comment use the `#` symbol.

.Comment example
====
[,typeql]
----
# This is the first query
insert # this is the insert query without a match clause
$p isa person; # this line inserts a person entity
$p has full-name "Bob"; # we add a constraint to the $p variable used in a previous line

# This is the second query
insert
$p isa person, has full-name "Alex"; # In this query we insert another person
----
====