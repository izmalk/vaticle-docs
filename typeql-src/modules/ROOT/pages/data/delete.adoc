= Delete query
:Summary: Deleting data from a TypeDB database.
:keywords: typeql, typedb, query, delete, remove
:pageTitle: Delete query

A Delete query removes data from a TypeDB database.
You can delete an instance of a type, ownership of an attribute,
or a role player reference of a relation.

Delete queries can be used only with write transactions.

== Behavior

A Delete query removes concepts, ownerships, and role player references from a database.

After matching existing data with a `match` clause,
use a `delete` clause with another TypeQL pattern to specify exact data to delete.

A Delete query doesn't return any useful information.

== Syntax

A Delete query consists of a
<<_match_clause,match>> clause and a
<<_delete_clause,delete>> clause.

Delete queries are written in TypeQL with the following syntax:

[,typeql]
----
match <pattern>
delete <pattern>
----

A `delete` clause pattern uses variables from a preceding `match` clause pattern.

[#_match_clause]
== Match clause

A `match` clause is mandatory in a Delete query.
You can use a declarative and composable TypeQL pattern in a `match` clause and TypeDB will find data that matches
specified pattern.

For more information on a `match` clause, see the
//#todo update the link after introducing the Match clause page!
xref:typeql::queries.adoc#_match[Match clause] description on the Queries page.

For more information on patterns used in a `match` clause, see the
xref:data/basic-patterns.adoc#_patterns_overview[Pattern syntax] section.

[#_delete_clause]
== Delete clause

A `delete` clause is used in a Delete query to specify exact data to remove from a database.
deleted data should be matched first in a preceding `match` clause.

A `delete` clause can have multiple statements in its pattern to delete in one query. But it can't delete types (use
xref:schema/modify.adoc#_undefine_a_type[undefine] to delete types) and can't have the following:

- Conjunction
- Disjunction
- Negation
- `is` keyword

This clause is executed exactly once for every result matched by the preceding `match` clause of the same query.
If there are no matches for a `match` clause in a `delete` query, then there will be no deletes.

A `delete` clause returns a Promise of an empty response.

[WARNING]
====
Prior to TypeDB v.`2.22.0`, a Delete query would fail (throw an error) if its `delete` clause would try to delete
a previously deleted or non-existent data.
====

=== Deleting instances

To delete an entity, attribute, or relation, you need to match them with a `match` clause and then
use the following pattern in a `delete` clause of the same query:

.Deleting an instance of data pattern syntax
[,typeql]
----
<variable> isa <type>;
----

Where `<variable>` (e.g., `$x`) -- a query variable of a matched instance to delete;
`<type>` (e.g., `person`) -- type or supertype of the deleted instance.

With this pattern, the instance of data is removed completely,
including all roles played and attributes owned by the deleted instance.

See an <<_simple_example,example>> of deleting instances of data below.

=== Deleting ownership

To delete an ownership of an attribute, match both the attribute and its owner and then use the following pattern
in a `delete` clause of the same query:

.Deleting an ownership pattern syntax
[,typeql]
----
<owner-variable> has <attribute-variable>;
----

With this pattern an attribute itself is not removed,
but rather an ownership of that attribute by that owner is removed.

=== Deleting role players from a relation

To delete a role player from a relation, match both the relation and the instance of data that plays a role
and then use the following pattern in a `delete` clause of the same query:

.Deleting a role player from a relation pattern syntax
[,typeql]
----
<relation-variable> (<role>:<instance-variable>);
----

Where `<relation-variable>` -- a query variable of the relation to modify;
`<role>` -- an optional specification of a role of a role player to remove;
`<instance-variable>` -- a query variable of a role player instance.

With this pattern, an instance that plays role in a relation is not removed,
but rather the information of that role player is removed from a relation.

== Examples

[#_simple_example]
=== Simple example

Use a match clause followed by the `delete` keyword and a pattern containing an `isa` statement to remove an entity
from a database.

[,typeql]
----
match
  $p isa person, has email "john.parkson@gmail.com";
delete
  $p isa person;
----

In the above query, `match` clause finds a `person` entity (or entities) owning an `email` attribute with a value of
`john.parkson@gmail.com`. Then, it removes the matched entities and all associated ownerships.

=== Relations

==== Instances

Use a match clause followed by the `delete` keyword and a pattern containing an `isa` statement to remove a relation
from a database.

[,typeql]
----
match
  $p isa subject, has full-name "Pearle Goodman";
  $a isa action, has name "modify_file";
  $ac (object: $o, action: $a) isa access;
  $pe (subject: $p, access: $ac) isa permission;
delete
  $pe isa permission;
----

The above query does the following:

. Finds a `subject` entity (`$p`), with `full-name` attribute value of `Pearle Goodman`.
. Finds an `action` entity (`$a`), with `name` attribute value of `modify_file`.
. Finds `access` relations (`$ac`) relating any `object` (as `object`) to the action `$a` (as `action`).
. Finds `permission` relations (`$pe`) relating the `subject` entity `$p` (as subject) to the `access`
relations `$ac` (as access).
. Deletes all matched permissions `$pe`.

In short, it removes the permissions that let Pearle Goodman modify files.

==== Role players

Use a match clause followed by the `delete` keyword and a pattern to remove a player from a role in a relation.

// - #todo Double-check the example

[,typeql]
----
match
  $p isa subject, has full-name "Masako Holley";
  $o isa object, has path "zewhb.java";
  $oo (owner: $p, owned: $o) isa object-ownership;
delete
  $oo (owner: $p);
----

The above query, assuming the `full-name` attribute is unique for each `subject` entity and the `path` attribute
unique for each `object`:

. Finds a `subject` entity (`$p`, `Masako Holley`).
. Finds an `object` entity (`$o`, `zewhb.java`).
. Finds an `owner` relation (`$oo`) relating `$p` (`owner`) to `$o` (`owned`).
. Deletes `$p` as a player of the `owner` role in `$oo`.

In short, it removes `Masako Holley` as an owner of the `zewhb.java` file. However, the relation itself stays, and any
other `subject` entities playing the `owner` role will continue to do so.

[NOTE]
====
The `isa object-ownership` statement is omitted because we are not deleting the `object-ownership` relation itself,
but rather a specific player of its `owner` role.
====

=== Attributes

Attributes can be owned by other types. A delete query can remove the attribute itself
or remove the ownership of it (and leave the attribute).

Attributes are immutable. Rather than changing the value of an owned attribute, the ownership of it is replaced with
the ownership of a new/different attribute.

==== Instances

Use a match clause followed by the `delete` keyword and a pattern containing an `isa` statement to remove an
attribute from a database.

[,typeql]
----
match
  $fn isa full-name;
  $fn “Bob”;
delete
  $fn isa full-name;
----

The above example finds the `full-name` attribute whose value is `Bob` and deletes it. As well as all ownerships of
this attribute by any entities, relations, or other attributes.

==== Ownership

TypeDB allows multiple instances to share the same attribute, so it is more common to remove the ownership of an
attribute rather than the attribute itself.

Use a match clause followed by the `delete` keyword and a pattern to remove the ownership of an attribute.

[,typeql]
----
match
  $o isa object, has path $fp;
  $fp like "(logs/.*)";
delete
  $o has $fp;
----

The above query finds all `object` entities that have a `path` attribute whose value matches a regular expression
(`logs/.*`). It then removes their ownership of any matching `path` attributes. However, the attributes
themselves are not removed.

[IMPORTANT]
====
Even a slight alteration of a `delete` clause can produce a very different result. Be careful not to delete the wrong
data accidentally. See the examples below.
====

The `isa object` statement in the query above is omitted because we are not deleting the `object` entities
but rather their ownership of `path` attributes.

For example, the `delete $o isa object, has $fp;` clause with a `match` clause above deletes all matched objects `$o`.
Thus, it deletes all their ownerships over any attributes, not only `$fp`.

We do not include `path` in the `delete` clause because it's not needed for ownership deletion. The type of `$fp` should
be specified in the `match` clause if it's important. In this case, it is specified as `path` already.

For example, the `delete $o has path $fp;` clause with a `match` clause above produces an error.

Finally, we can delete the attributes themselves, thus deleting ownership over any of them by all instances of all
types.

For example, the `delete $fp isa path;` clause with a `match` clause above deletes all matched `path` attributes, thus
deleting all ownerships of these attributes from every owner of any type.
