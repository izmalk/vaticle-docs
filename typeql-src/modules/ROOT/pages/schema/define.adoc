= Define

See the xref:typeql::schema/schema.adoc[] page for more information about
xref:typeql::schema/schema.adoc#_schema[schema]
and xref:typeql::schema/schema.adoc#_types[types].

////
.See the full table of contents for this page
[%collapsible]
====
toc::[]
====
////

To use a new TypeDB database, we need to define its schema first. We can do it with a <<#_define_query>>.

[IMPORTANT]
====
To define a schema, a `schema` xref:typedb:ROOT:development/connect.adoc#_sessions[session] must be opened,
and a `write` xref:typedb:ROOT:development/connect.adoc#_transactions[transaction] started.
The changes must be *committed*, or they will NOT be permanent.
====

There is no limitation in the order of types to define. We can define schema types in any order if the schema is valid.
TypeDB Clients validate our schema definition requests before sending them to a TypeDB
server. We will not be able to commit changes if the resulting schema isn't valid.

// tag::idempotence[]
[NOTE]
====
Define queries are idempotent.

Running the same define query a second time shall not produce any changes to the database schema.

Running a modified version of an already executed schema definition query can add concepts to the
schema but mostly can't modify existing ones. TypeQL schema statements do NOT replace existing type definitions but
add missing parts.

Two notable exceptions are:

- **rules** (defining a new rule with an existing label/name will replace the existing rule completely), and
- **annotations**, like the `@key` keyword (which we can remove by redefining attribute ownership without the
  annotation).
====
// end::idempotence[]

[#_define_query]
== Define query

A define query adds new or modifies existing types and/or rules in the database schema.

A define query consists of a single `define` clause.

TypeQL `define` clause must begin with the `define` keyword.

We can run multiple statements as a single query.
The `define` keyword must only be included once per query at the beginning.

[IMPORTANT]
====
This page only covers how to define types.
For defining rules, see the xref:typeql::schema/rules.adoc[] page.
====

=== Syntax

Define queries are written in TypeQL with the following syntax:

[,typeql]
----
define <pattern>
----

The pattern used in the define query is limited to the DDL part of TypeQL as it declares only types and rules,
but not instances of data.

[#_type_definition_syntax]
=== Type definition syntax

Types are defined in TypeQL with the following syntax:

[,typeql]
----
define

<label> sub <supertype labell>
[(, abstract)]
[, value <value-type>]
(, relates <role label>)
[(, relates <role label>)...]
[(, owns <attribute type label> [@annotation])...]
[(, plays <relation type label>:<role>)...];
----

The line with the `value` keyword is relevant only for attribute types definition.

The lines with the `relates` keyword are relevant only for relation types definition.

See the specific syntax for
<<_define_entity_types,entity types>>,
<<_define_relation_types,relation types>>, or
<<_define_attribute_types,attribute types>> below.

[#_rule_definition_syntax]
=== Rule definition syntax

Rules are defined in TypeQL with the following syntax:

[,typeql]
----
rule <label>:
when {
    <pattern>
} then {
    <pattern>
};
----

Use the `rule`, `when`, and `then` keywords for a rule definition.

[IMPORTANT]
====
Unlike the <<_type_definition_syntax>>, the rule syntax uses patterns consisting of
variables and constraints for **data** instances. For more information on these patterns,
see the xref:queries/match.adoc#_patterns_overview[Pattern syntax] section on the xref:typeql::queries/match.adoc[] page.
====

=== Example

////
[IMPORTANT]
====
The following examples define parts of the xref:typedb:ROOT:tutorials/iam-schema.adoc[IAM schema] that will be used throughout
the rest of the documentation. However, some queries on this page result in modification of the original IAM schema
used in the Quickstart guide. These occasional modifications are needed to demonstrate what TypeQL is capable of.

To avoid any errors and/or data loss it is recommended to use a separate database for all examples on this page. For
more information on creating a new database, see the xref:typedb:ROOT:quickstart-guide.adoc#_create_a_database[Quickstart guide].
====
////

[,typeql]
----
define

credential sub attribute, value string;
full-name sub attribute, value string;
id sub attribute,
    abstract,
    value string;
email sub id, value string;

subject sub entity,
    abstract,
    owns credential,
    plays group-membership:group-member;

user sub subject,
    abstract;

person sub user,
    owns full-name,
    owns email;

membership sub relation,
    abstract,
    relates parent,
    relates member;

group-membership sub membership,
    relates group as parent,
    relates group-member as member;
----

The above example can be run in xref:clients:ROOT:studio.adoc[TypeDB Studio]. See the
xref:typedb:ROOT:quickstart-guide.adoc[Quickstart guide] for full descriptions of the following steps:

. Make sure a TypeDB server is up.
. Start TypeDB Studio.
. Connect TypeDB Studio to the server.
. Create or select a new database.
. Open a Project folder.
. Copy and paste the query code above into a new tab of the *Text editor*.
. Ensure the btn:[Session type] switch is set to "*Schema*".
. Ensure the btn:[Transaction type] switch is set to "*Write*".
. Click the green "play" button to start the transaction.
. Click the "checkmark" button to commit the changes.

//#todo insert real images of icons above

After a successful commit, all the types can be seen in the Type Browser panel in the bottom left part of the Studio
window.

The following query can be run in a new tab in *Schema* / *Read* mode to see the schema visualized as a graph.

[,typeql]
----
match $s sub thing;
----

As the result of the above query TypeDB Studio should display a graph visualization of the schema defined earlier.

image::schema-example-1.png[Schema visualization example]

include::typedb::partial$thing-warning.adoc[]

In the following sections, we can find more detailed information on different schema definition queries and
possibilities.

[#_define_entity_types]
== Define entity types

=== Overview

Entity types are defined independently of other types, but may subtype other entity types.

Optionally, entity types can:

* Be abstract.
* Own attribute types.
* Play roles defined in relation types.

=== Syntax

Entity types are defined in TypeQL with the following pattern:

[,typeql]
----
<label> sub (entity | <entity type label>) [(, abstract)]
[(, owns <attribute type label> [@annotation])...]
[(, plays <relation type label>:<role>)...];
----

=== Examples

==== Basic

The following `define` query creates a new entity type, `object`, by subtyping the `entity` built-in type.

[,typeql]
----
define

object sub entity;
----

[#_abstract_types]
==== Abstract

Optionally, types can be defined as `abstract`. An abstract type can't be instantiated and must be
subtyped in order to create entities. It exists only so other types can inherit ownerships over attribute types it
owns and the roles it plays. We can use abstract types in queries, expecting to retrieve instances of its concrete
subtypes.

[,typeql]
----
define

object sub entity,
    abstract;
----

==== Owns an attribute

To define a new entity type that owns one or more attribute types, use the `owns` keyword followed by the label of the
attribute type. The attribute types are appended to the entity type definition with commas. Note, attribute types must
be defined before or concurrently (in the same query) with its owner(s). We can add owners later, but we can't
own nonexistent attribute types.

[,typeql]
----
define

object-type sub attribute, value string;

object sub entity,
    abstract,
    owns object-type;
----

[#_cardinality]
===== Cardinality

By default, an entity can have multiple attributes of the same type: zero, one, or many.

By having multiple attributes of the same type we're effectively creating a
xref:typeql::queries/insert.adoc#_multivalued_attributes[multivalued attribute] (as if having an attribute type instantiated
with multiple values).

Use the `@key` keyword to limit the cardinality to exactly *one* and add *uniqueness* constraint.
Hence, the instance of the type with key ownership will have exactly one (no more and no less)
key attribute instance.

See the example in the <<_key_attribute,Key attribute>> section.

==== Plays a role

To add roles that entities of a specific entity type can play, use the `plays` keyword.

[,typeql]
----
define

access sub relation,
    relates object;

object sub entity,
    abstract,
    plays access:object;
----

==== Subtypes another entity

[NOTE]
====
All types that are subtyping `entity` built-in type directly or through other subtypes are called entity types.
Instances of entity types are called entities. A similar terminology is applied to attributes and relations.
====

An entity type can subtype another entity type by using the same `sub` keyword, but replacing the `entity` keyword
after it with a label of another entity type to subtype.

[,typeql]
----
define

path sub attribute, value string;

object sub entity,
    abstract,
    owns object-type,
    plays access:object;
resource sub object,
    abstract;
file sub resource,
    owns path;
----

In the above example, the `resource` and `file` entity types are subtypes of the `object`, which itself is a subtype
of the `entity` built-in type. They inherit the `object-type` attribute type ownership from it as well as its
`access:object` role. However, while the `resource` subtype is abstract, the `file` subtype is not. Hence, we
can create `file` entities, but not `resource` entities.

Further, the `path` attribute type will only be owned by the `file` entity type and any other entity types which
subtype it or directly define ownership.

==== Overrides inherited attribute ownership

To override an inherited ownership use `owns` keyword with the new attribute type label, followed by the `as` keyword
and the inherited attribute type label. For example:

[,typeql]
----
define file sub resource, owns file-type as object-type;
----

The new attribute type that overrides inherited type is defined in the schema as subtype of the inherited
attribute type. Hence, the inherited attribute type is abstract and has the same value type as the new attribute type.
The example above in a schema would look like this:

[,typeql]
----
define

path sub attribute, value string;

object-type sub attribute, abstract, value string;
file-type sub object-type, value string;

object sub entity,
    abstract,
    owns object-type;
resource sub object,
    abstract;
file sub resource,
    owns path,
    owns file-type as object-type;
----

In the above example, the `file` entity type owns the `path` and `file-type` attribute types, with the `file-type`
attribute type overriding the inherited `object-type` attribute type.

[#_define_relation_types]
== Define relation types

=== Overview

Relation types are defined independently of other types but may subtype other relation types. Their definition can
include ownership of attribute types, roles other types play within them, and roles they can play in other relation
types:

* Owned attribute types are added with the `owns` keyword followed by the attribute type label.
* Its own roles are added with the `relates` keyword followed by the role label. At least one role must be defined for
any relation.
* Roles it can play in other relations are added with the `plays` keyword followed by the relation type label and role.

=== Syntax

Relation types are defined in TypeQL with the following pattern:

[,typeql]
----
<label> sub (relation | <relation type label>) [(, abstract)]
[(, owns <attribute type label> [@annotation])...]
(, relates <role label>)
[(, relates <role label>)...]
[(, plays <relation type label>:<role>)...];
----

=== Examples

==== Basic

The following statement creates an `access` relation that defines two roles:

* `object` -- played by instances of the `object` entity type or its subtypes (e.g.,`file`).
* `action` -- played by instances of the `action` entity type.

[,typeql]
----
define

access sub relation,
    relates object,
    relates action;
----

==== Plays a role

In addition to defining its own roles played by other types, a relation type can play roles in other relation types.

[,typeql]
----
define

access sub relation,
    relates object,
    relates action,
    plays permission:access,
    plays change-request:change;
----

In the above example, `access` relation type can play the `access` role in `permission` relation type and
the `change` role in `change-request` relation type. Besides, an `access` relation type relates an
`object` role (e.g., file) and a `action` role (e.g., read). Thus, a `permission` relation type relates
the `access` (i.e., read + file) and a `subject` (e.g., `person` with `full-name` attribute `Kevin Morrison`).

==== Defines multiple roles

A relation can define multiple roles (from one to many).

[,typeql]
----
define

change-request sub relation,
    relates change,
    relates requestee,
    relates requester;
----

==== Owns an attribute

A relation type can own zero, one, or many attribute types.

[,typeql]
----
define

segregation-policy sub relation,
    relates action,
    plays segregation-violation:policy,
    owns name;
----

==== Abstract

Optionally, relation types can be defined as `abstract`, so they must be subtyped in order to create instances. An
abstract relation type exists only so other relation types can inherit the attribute types it owns and the roles it
defines and plays.

[,typeql]
----
define

violation sub relation,
    abstract,
    owns name;
----

==== Subtypes another relation

A relation type can subtype another relation type by replacing the `relation` keyword with the label of another
relation type. The subtype will inherit all owned attribute types and all roles related or played by the parent type.

[,typeql]
----
define

membership sub relation,
    relates parent,
    relates member;

collection-membership sub membership;
----

In the example above, the `collection-membership` relation type inherits the `parent` and `member` roles defined in
its parent type: `membership`.

[#_overrides_a_role]
==== Overrides a role

The labels of the inherited roles can be overridden to distinguish between the roles inherited by a relation subtype
versus the roles defined by its parent type.

[,typeql]
----
define

membership sub relation,
    relates parent,
    relates member;

collection-membership sub membership,
    relates collection as parent;
----

In the example above, the `collection-membership` relation type subtypes the `membership` relation type and overrides
the inherited `parent` role as `collection`. The inherited `member` role is inherited as it is.

[NOTE]
====
The two examples above can be run one after another. The second one will update the `collection-membership` type to
override one of its inherited roles.
====

[IMPORTANT]
====
By overriding an inherited role, we implicitly prevent the sub-relation from relating the role that would otherwise be
inherited.
====

//#todo add another example on inherited overriden roles:

==== Complex example

[,typeql]
----
define

ownership sub relation,
    relates owned,
    relates owner;

group-ownership sub ownership,
    relates group as owned,
    owns ownership-type;

object-ownership sub ownership,
    relates object as owned,
    owns ownership-type;

access sub relation,
    relates object,
    relates action,
    plays change-request:change;

change-request sub relation,
    relates requester,
    relates requestee,
    relates change;
----

//#todo add illustration for this example

The example above defines one attribute type and five relation types:

* `ownership` -- subtypes the `relation` built-in type, and relates `owned`, and `owner` roles.
* `group-ownership` -- subtypes `ownership` relation type, relates `group` as `owned`, and `owner` (inherited).
* `object-ownership` -- subtypes `ownership` relation, relates `object` as `owned`, and `owner` (inherited).
* `access` -- subtypes the `relation` built-in type, relates `object` (e.g., file) and `action` (e.g.,
  read), plays the role of `change` in `change-request` relation type.
* `change-request` -- subtypes the `relation` built-in type, relates `requester`, `requestee` and `change`.

[#_define_attribute_types]
== Define attribute types

=== Overview

Attribute types are defined independently of other types, but may subtype a user-defined abstract attribute type.
Any type can have an ownership over any attribute type.

[WARNING]
====
Attributes owning an attributes feature will be deprecated in one of the upcoming versions and deleted in TypeDB
version 3.0.
====

[WARNING]
====
Attributes playing a role in a relation feature will be deprecated in one of the upcoming versions and deleted in
TypeDB version 3.0.
====

Optionally, attribute types can:

* Be abstract.
* Own other attribute types (this will be deprecated).
* Play roles in relations (this will be deprecated).

=== Syntax

Attribute types are defined in TypeQL with the following pattern:

[,typeql]
----
<label> sub (attribute | <abstract attribute type label>) [(, abstract)], value <value type> [, regex "<regex-expression>"]

[(, owns <attribute type label> [@annotation])...]

[(, plays <relation type label>:<role>)...];
----

[#_value_types]
The following *value types* are supported:

* `long` -- a 64-bit signed integer
* `double` -- a double-precision floating point number, including a decimal point
* `string` -- enclosed in double `"` or single `'` quotes
* `boolean` -- `true` or `false`
* `datetime` -- a date or date-time in the following formats:
 ** `yyyy-mm-dd`
 ** `yyyy-mm-ddThh:mm`
 ** `yyyy-mm-ddThh:mm:ss`
 ** `yyyy-mm-ddThh:mm:ss.f`
 ** `yyyy-mm-ddThh:mm:ss.ff`
 ** `yyyy-mm-ddThh:mm:ss.fff`

=== Examples

==== Basic

[,typeql]
----
define

name sub attribute, value string;
email sub attribute, value string;
ownership-type sub attribute, value string;
review-date sub attribute, value datetime;
validity sub attribute, value boolean;
----

==== Subtypes another attribute type

An attribute type can subtype another attribute type if its *abstract*. This is useful when the possible values of
an attribute type can be categorized, and applications can benefit from querying entities and relations not only by
a value of an attribute but also by a label of attribute type.

[IMPORTANT]
====
An attribute type can only subtype an abstract attribute type. However, the subtype of an attribute type can itself be
abstract. Further, an attribute subtype must have the same **value type** as its parent attribute type. Note, the
**value type** of an attribute subtype can be omitted in its definition. It will be inherited from its parent
attribute type.
====

[,typeql]
----
define

id sub attribute, abstract, value string;
email sub id, value string;
name sub id, value string;
path sub id, value string;
number sub id, value string;
----

The above example creates an attribute type, `id`. However, because different entities can be identified by different
information, the `id` type is subtyped by `email`, `name`, `path`, and `number` types -- making it possible to query
users by `email`, business units by `name`, files by `path` and records by `number`.

[#_unique_attribute]
==== Unique attribute annotation

Use the `@unique` keyword to add *uniqueness* constraint to the ownership of an attribute.
This prevents two instances of the same type from owning the same attribute instance (with the same value).

[,typeql]
----
define

object-type sub attribute, value string;

object sub entity,
    abstract,
    owns object-type @unique;
----

[IMPORTANT]
====
The `@unique` annotation allows us to declare that an attribute instance may not be owned more than once by the
owner type. But no <<_cardinality,cardinality>> restriction is generated from `@unique` annotation.
====

Unique annotation can be inherited, even using override of an ownership.

.See example
[%collapsible]
====
[,typeql]
----
define

organisation sub entity,
    abstract,
    owns organisation-id @unique;

organisation-id sub attribute, abstract, value string;

non-profit sub organisation,
    owns nonprofit-id as organisation-id;

nonprofit-id sub organisation-id, value string;
----
In this example `non-profit` owns `nonprofit-id` with unique constraint. It's inherited from `organizatrion-id`.
====

[#_key_attribute]
==== Key attribute annotation

Use the `@key` keyword to set the owned attribute as key.
That limits the cardinality to exactly *one* and adds *uniqueness* constraint.
Hence, the instance of the type with attribute ownership with `@key` annotation will have exactly one
(no more and no less) key attribute instance, and it will be unique for all instances of the same type.

[,typeql]
----
define

object-type sub attribute, value string;

object sub entity,
    abstract,
    owns object-type @key;
----

[WARNING]
====
An ownership can't have both `@unique` and `@key` at the same time.
====

==== Regular expressions

The values of an attribute type can be restricted using Java regular expressions. For example, to constrain it to a
set of options.

[,typeql]
----
define

visibility sub attribute, value string, regex "^(public|private|closed)$";
----

The above example defines an attribute type: `visibility`. It is intended for user groups, and specifies a regex to
restrict its values to `public`, `private` and `closed`.

==== Owns other attribute types

While it is more common for entity and relation types to own attributes, attribute types can also own (other)
attribute types.

[WARNING]
====
Attributes owning an attributes feature will be deprecated in one of the upcoming versions and deleted in TypeDB
version 3.0.
====

[,typeql]
----
define

symlink sub attribute, value string;

filepath sub attribute,
    value string,
    owns symlink;
----

The above example creates an attribute type `filepath`, intended for files. It is assumed there can be multiple copies
of a `file`, each with its own `filepath` -- and symlinks can be created that point to these filepaths. Thus, the
`filepath` attribute type (and NOT the `file` entity type) owns the `symlink` attribute type.

==== Plays a role

While it is more common for the roles in relations to be played by entities or other relations, they can also be played
by attributes.

[WARNING]
====
Attributes playing a role in a relation feature will be deprecated in one of the upcoming versions and deleted in
TypeDB version 3.0.
====

[,typeql]
----
define

credential sub attribute,
    value string,
    plays change-request:change;
----

The above example creates the `credential` attribute type, and specifies it can play the role of `change` in
the `change-request` relation type. While `change-requests` were intended to manage access changes, they can now be
used to manage `credential` changes as well.

== Define rules

For syntax, see the <<_rule_definition_syntax>> section above.

Rules are defined independently of types in the same schema.
However, any types used in a rule must be defined in the schema.

They are executed only as a part of xref:typeql::queries/get.adoc[Get queries] when the
xref:typedb:ROOT:development/infer.adoc[inference] option is *enabled* in a *read* transaction.

[IMPORTANT]
====
The results inferred by rules are virtual because they exist only within the transaction. They are not persisted
in a database, and any data inferred in a query ceases to exist when the transaction is closed.
====

Read more on rules in the <<_rule_validation>> section below.

[#_rule_validation]
=== Rule validation

// tag::rule_validation[]
A rule's `when` clause (condition) can be a pattern with multiple statements, including disjunctions and negations.

A `then` clause (conclusion) describes a <<_single_conclusion,single>> relation or a constraint of ownership of
an attribute (due to https://en.wikipedia.org/wiki/Horn_clause[Horn-clause logic,window=_blank]).

When using a disjunction in a rule, the disjunctive parts must be bound by variables outside the `or` statement.
These variables are the only ones permitted in the `then` clause.

Since version `2.18.0`, we can use xref:typeql::schema/types.adoc#_abstract_types[abstract] types in a rule
as long as all the type variables that define which instances to create during materialization are concrete
(non-abstract).

.See the example with abstract types
[%collapsible]
====
[,typeql]
----
define

abstract-person sub entity, abstract, plays friendship:friend; #abstract
friendship sub relation, relates friend;  #non-abstract

rule concrete-relation-over-abstract-players:
when {
   $x isa abstract-person;
} then {
   (friend: $x) isa friendship;
};
----
====

The `then` clause of a rule can't use variables (either concept variables or value variables) that aren't defined in
the `when` clause.

The `then` clause of a rule must not insert any instance which occurs negated in its `when` clause or in the `when`
clause of any rule it may trigger. Attempting to define such a rule will throw an error.

Rules will not create duplicates of instances that are already in the database or have already been inferred.
There is no need to check if it already exists in a rule.

[#_single_conclusion]
There are exactly *three* distinct *conclusions* permitted:

. A new relation.
. Ownership of an attribute defined by its value.
. Ownership of an attribute defined by a variable.

The `then` clause must be insertable according to the schema (e.g., we can't give an attribute to an instance
that can't own that attribute type).
// end::rule_validation[]

=== Rule processing (inference)

The approach TypeDB uses is a https://en.wikipedia.org/wiki/Backward_chaining[backward-chaining,window=_blank]
execution on top of https://en.wikipedia.org/wiki/Horn_clause[Horn-clause,window=_blank] logic.

Negation functionality follows the set-difference semantics. It corresponds to the negation-as-failure model under the
following conditions:

- We have stratified negation.
- The results are grounded.
- We ensure all variables occurring both inside and outside the negation are bound by the time the negation is
evaluated.

Infinite recursion and non-termination are prevented with a tabling mechanism.

=== Examples

==== Basic

[,typeql]
----
define

rule test:
    when {
        $p isa person;
    } then {
        $p has full-name "Dude";
    };
----

The example above demonstrates a simple rule. All `person` entities matched by a read query with the inference option
*enabled* will have a `full-name` attribute with the value `Dude`, even if they have other `full-name`
attributes with different values.

==== Using value variables

[,typeql]
----
define

size-mb sub attribute, value double;

file owns size-mb;

rule size-covert:
    when {
        $f isa file, has size-kb $s;
        ?mb = $s/1024;
    } then {
        $f has size-mb ?mb;
    };
----

The above query defines an additional attribute subtype `size-mb`, defines that it can be owned by the `file` entity
subtype, and creates a rule `size-convert` to create ownership of `size-mb` with the value *1024* times lower than
`size-kb` to any `file` instance that has `size-kb`. The rule uses a
xref:typeql::queries/match.adoc#_value_variables[value variable] for
xref:queries/match.adoc#_arithmetic_operations[arithmetics].

With this rule defined and the inference option xref:typedb:ROOT:development/infer.adoc#_how_to_use_inference[enabled],
we can try the following query:

[,typeql]
----
match
    $f isa file, has size-kb $s, has size-mb $mb;
    $mb > 1;
----

With the default IAM dataset sample, this query shall return only one result (because all others will have `size-mb`
lower than 1), similar to this:

[,typeql]
----
{
    $f iid 0x826e80078000000000000000 isa file;
    $mb 1.6650390625 isa size-mb;
    $s 1705 isa size-kb;
}
----

[IMPORTANT]
====
The value of `size-mb` is not persisted in the database but instead -- inferred by the `size-covert` rule every time
we do a read transaction with the inference option enabled and query for `size-mb`.
====

==== Transitive rule

[,typeql]
----
define

rule transitive-reachability:
    when {
        (from: $x, to: $y) isa rel;
        (from: $y, to: $z) isa rel;
    } then {
        (from: $x, to: $z) isa rel;
    };
----

The example above allows for the transitivity of relations. We can interpret this rule as joining two relations
together. It creates a relation `x` to `z`, given that there are relations of `x` to `y` and `y` to `z`.

==== Advanced transitivity usage

When inferring relations, it is possible to variablize any part of the `then` clause of the rule. For example, if we
want a rule to infer many types of relations, we could propose a rule such as:

[,typeql]
----
define

rule all-relation-types-are-transitive:
    when {
        ($role1: $x, $role2: $y) isa! $relation;
        ($role1: $y, $role2: $z) isa! $relation;
    } then {
        ($role1: $x, $role2: $z) isa $relation;
    };
----
//#todo Add advanced transitivity page or blog post here

==== Complex rule

[,typeql]
----
define

rule add-view-permission:
    when {
        $modify isa action, has name "modify_file";
        $view isa action, has name "view_file";
        $ac_modify (object: $obj, action: $modify) isa access;
        $ac_view (object: $obj, action: $view) isa access;
        (subject: $subj, access: $ac_modify) isa permission;
    } then {
        (subject: $subj, access: $ac_view) isa permission;
    };
----

The example above illustrates a more complex rule using the IAM schema.

In short, the permission to access some file with the action that has `name` of `view_file` can be inferred by the
rule from the permission to `modify_file` the same file.

The xref:typedb:ROOT:development/infer.adoc#_example[Example] section of Inferring data page gives a full explanation
of how this rule works.

== Modify existing schema

=== Add a type or a rule

We can add types and rules to an existing schema by running new define statements to the same database as usual.

The define statements are idempotent. By sending the same define query twice or more times, the resulting
schema must be achieved as if we send it only once. So types and/or rules will not be duplicated.

A separate define statement for a new type or a rule can be sent as a `define` query. Alternatively, the statement
can be added to the define statement of the existing schema and sent together. Only new types and/or rules will be
added in this case. If we change the name (label) of the existing type or rule in the existing schema and then send it
as a define query, then the changed type or rule will be processed as a new one.
//#todo Add examples. Step by step adding constraints.

include::typeql::schema/define.adoc[tag=idempotence]

=== Delete a type or a rule

Use the `undefine` keyword to remove the definition of a type or its association with other types from the schema.

[IMPORTANT]
====
Don't forget to `commit` after executing an `undefine` statement. Otherwise, any changes are NOT committed to a database.
====

=== Rename a type

// tag::rename[]
To rename a type (to change its label), use the xref:clients:ROOT:studio.adoc[TypeDB Studio] or
xref:typedb:ROOT:development/api.adoc[TypeDB Driver API] rename method for a type class object:
xref:clients::java/java-api-ref.adoc#_rename_type_label[Java],
xref:clients::python/python-api-ref.adoc#_rename_type_label[Python],
xref:clients::node-js/node-js-api-ref.adoc#_rename_type_label[Node.js].
// end::rename[]

=== Modify a rule

To modify a rule, xref:typeql::schema/rules.adoc[define a new rule] with the same label.
It will overwrite the existing rule upon commit.
