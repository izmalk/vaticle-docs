= Lesson 7.3: Disjunctions and negations

== Disjunctions

By default, all statements in a pattern are joined in a https://en.wikipedia.org/wiki/Logical_conjunction[conjunction]. As we have seen in xref:learn::7-understanding-query-patterns/7.1-patterns-as-constraints.adoc[Lesson 7.1], this means that the pattern must be satisfied as a set of simultaneous constraints. If we want to create a https://en.wikipedia.org/wiki/Logical_disjunction[disjunction], we must use a *disjunction block*. We do so in the following Fetch query, where we retrieve a list of books that a user "likes". We consider a user to have liked a book if either they have ordered it or they have reviewed it with a score of seven or higher.

[,typeql]
----
match
$user isa user, has id "u0008";
$liked-book isa book;
{
    ($user, $order) isa action-execution;
    ($order, $liked-book) isa order-line;
} or {
    ($user, $review) isa action-execution;
    ($review, $liked-book) isa rating;
    $review has score >= 7;
};
fetch
$liked-book: isbn-13;
----

We have encountered *blocks* before when we used them to define rules in xref:learn::5-defining-schemas/5.4-defining-rules.adoc[Lesson 5.4]. To recap, blocks are delimited by braces and contain patterns. When a block is used as part of larger pattern, for instance in a `match` clause as we have done above, the pattern contained in the block is a *sub-pattern*.

To construct a disjunction within a query pattern, we place each branch of the disjunction inside a block and interleave the blocks with the `or` keyword. In the example above, we have used a disjunction with two branches. The final block of a disjunction must be followed with a `;` delimiter, signalling the end of the disjunction. As with all TypeQL syntax elements, whitespace and indentation does not affect the functionality of a disjunction.

.Exercise
[caption=""]
====
Write a Fetch query to retrieve the ISBN-13s of all books that are included in an order that has a status of "paid", "dispatched", or "delivered".

You may find it useful to refer to the bookstore's schema.

.Schema
[%collapsible]
=====
[,typeql]
----
include::learn::attachment$bookstore-schema.tql[lines=18..]
----
=====

.Sample solution
[%collapsible]
=====
[,typeql]
----
match
$book isa book;
$order isa order;
($book, $order) isa order-line;
{
    $order has status "paid";
} or {
    $order has status "dispatched";
} or {
    $order has status "delivered";
};
fetch
$book: isbn-13;
----
=====
====

== Negations

Like we can represent a disjunction of patterns using a disjunction block, we can represent the https://en.wikipedia.org/wiki/Negation[negation] of patterns with a *negation block*. The following Fetch query retrieves a list of books that do not have illustrators.

[,typeql]
----
match
$book isa book;
not {
    ($book, $contributor) isa illustrating;
};
fetch
$book: isbn-13;
----

To construct a negation within a query pattern, we place the sub-pattern inside a block preceded by the `not` keyword and followed by a `;` delimiter.


[IMPORTANT]
====
All sub-patterns must have at least one variable that is shared with the pattern outside the containing block. Additionally, variables that _only_ appear within a sub-pattern cannot be accessed in other clauses of the query.
====

.Exercise
[caption=""]
====
Write a Fetch query to retrieve the ISBN-13s of books that the user with ID "u0008" has neither ordered nor reviewed, indicating that the book would likely be new to that user.

You may find it useful to refer to the bookstore's schema.

.Schema
[%collapsible]
=====
[,typeql]
----
include::learn::attachment$bookstore-schema.tql[lines=18..]
----
=====

.Sample solution
[%collapsible]
=====
[,typeql]
----
match
$user isa user, has id "u0008";
$new-book isa book;
not {
    ($user, $order) isa action-execution;
    ($order, $new-book) isa order-line;
};
not {
    ($user, $review) isa action-execution;
    ($review, $new-book) isa rating;
};
fetch
$new-book: isbn-13;
----

Here, we have used a conjunction of negations, but we could identically use a negation of disjunctions by https://en.wikipedia.org/wiki/De_Morgan%27s_laws[De Morgan's laws]!

[,typeql]
----
match
$user isa user, has id "u0008";
$new-book isa book;
not { {
    ($user, $order) isa action-execution;
    ($order, $new-book) isa order-line;
} or {
    ($user, $review) isa action-execution;
    ($review, $new-book) isa rating;
}; };
fetch
$new-book: isbn-13;
----
=====
====
