= Lesson 3.4: Fetching schema types

== Variablizing types

So far, we've seen that the output of Fetch queries includes a `type` field for every retrieved data instance and their attributes. However, there are also types involved in the query that are not returned. Consider this polymorphic query from xref:learn::3-reading-data/3.2-fetching-polymorphic-data.adoc[Lesson 3.2], where we asked for books that a user had interacted with in some way on the web store.

[,typeql]
----
match
$user isa user, has id "placeholder id";
$book isa book;
(executor: $user, action: $action) isa action-execution;
($book, $action) isa relation;
fetch
$book: isbn, title;
----

[,json]
----
result placeholder
----

While the results list the books, they do not tell us anything about the nature of the user's interaction with them. We can easily retrieve this information by adding a couple of additional lines to the query.

[,typeql]
----
match
$user isa user, has id "placeholder id";
$book isa book;
(executor: $user, action: $action) isa action-execution;
($book, $action) isa relation;
$action isa $action-type;   # new line
fetch
$book: isbn, title;
$action-type;               # new line
----

If we *run* image:learn::studio-icons/run.png[] this new query, we now also retrieve the type of `$action`!

[,json]
----
result placeholder
----

We have been able to retrieve this information by introducing a new `$action-type` variable in an additional statement.

[,typeql]
----
$action isa $action-type;
----

Previous `isa` statements we've used have used a variable as the https://en.wikipedia.org/wiki/Subject%E2%80%93verb%E2%80%93object_word_order[subject] of the statement and a *type label* as the https://en.wikipedia.org/wiki/Subject%E2%80%93verb%E2%80%93object_word_order[object], but in this `isa` statement we've also used a variable, `$action-type`, as the object. The `isa` keyword must always take a data instance as the subject and a type as the object, which means that the variable `$action-type` represents a type rather than a data instance! Here we've made use of *type variablization*, one of TypeQL's most powerful features.


[#_exercise_3_4_1]
.Exercise
[caption=""]
====
Modify the above query to also retrieve the type of the relation between `$book` and `$action`.

.Sample solution
[%collapsible]
=====
[,typeql]
----
match
$user isa user, has id "placeholder id";
$book isa book;
(executor: $user, action: $action) isa action-execution;
($book, $action) isa $relation-type;
$action isa $action-type;
fetch
$book: isbn, title;
$action-type;
$relation-type;
----
=====

====

== Variablizing roles

The ability to variablize types in queries arises from TypeQL's core philosophy:

[quote]
____
https://typedb.com/fundamentals/type-theory[_Everything has a type, and so everything can be a variable._]
____

In fact, every element of `match` clauses in TypeQL queries can be variablized except for the keywords. This means that we can also take advantage of *role variablization*. In the following query, we retrieve a list of contributors to a particular book.

[,typeql]
----
match
$book isa book, has isbn "isbn placeholder";
$contributor isa contributor;
$contribution (work: $book, contributor: $contributor) isa contribution;
fetch
$contributor: name;
----

[,json]
----
result placeholder
----

However, the results do not tell us how each person contributed to the book. To retrieve this information, we can replace the role name `contributor` with a variable.

[,typeql]
----
match
$book isa book, has isbn "isbn placeholder";
$contributor isa contributor;
$contribution (work: $book, $contributor-role: $contributor) isa contribution;
fetch
$contributor: name;
$contributor-role;
----

[,json]
----
result placeholder
----

Now we can see each contributor's role in the results! When not part of a relation tuple, roles are described by *role labels*, comprising two terms specified by a `:` delimiter. The first term is the name of the relation that the role is part of, and the second is the name of the role in the scope of the relation. This is because multiple relations are permitted to use the same role names. So, for instance, the label `authoring:author` represents the `author` role of the `authoring` relation type.

This tells us something important about the results we have been retrieving: the `contribution` relations the query has matched are not all of the same type. The `contribution` relation type actually has three subtypes: `authoring`, `editing`, and `illustrating`, and we have been matching all of them using inheritance polymorphism!

.Exercise
[caption=""]
====
Modify the solution to the <<#_exercise_3_4_1,last exercise>> to also retrieve the roles that `$book` and `$action` play in the variablized relation.

.Sample solution
[%collapsible]
=====
[,typeql]
----
match
$user isa user, has id "placeholder id";
$book isa book;
(executor: $user, action: $action) isa action-execution;
($book-role: $book, $action-role: $action) isa $relation-type;
$action isa $action-type;
fetch
$book: isbn, title;
$action-type;
$relation-type;
$book-role;
$action-role;
----
=====

====

== Querying type hierarchies

In addition to using type variablization to retrieve the types of data instances returned in our queries, we can also query the schema directly, without having to involve data instances at all. For example, the following query retrieves the subtypes of `book`.

[,typeql]
----
match
$book-type sub book;
fetch
$book-type;
----

Try *running* image:learn::studio-icons/run.png[] this query. You should see the following result.

[,json]
----
result placeholder
----

These are indeed the subtypes of `book`. We also retrieve the type `book` itself because it is trivially its own subtype. For this query, we've used the `sub` keyword, which matches subtypes of a given supertype. Unlike the `isa` keyword, both the subject and the object of `sub` statements must be *types*. In this case, we've variablized the subject of the `sub` statement to query subtypes, but we could also variablize the object to query supertypes. In the following example, we query the supertypes of `publisher`.

[,typeql]
----
match
publisher sub $supertype;
fetch
$supertype;
----

[,json]
----
result placeholder
----

In addition to `company` (which is the direct supertype of `publisher`) and `publisher` (which is trivially its own supertype) we also retrieve the *root types* `entity` (the supertype of all entities) and `thing` (the supertype of _all_ types).

[WARNING]
====
The `thing` type is deprecated and will be removed in TypeDB 3.0.
====

.Exercise
[caption=""]
====
Determine the direct supertype of `city` by writing a query to retrieve its supertypes.

.Sample solution
[%collapsible]
=====
[,typeql]
----
match
city sub $supertype;
fetch
$supertype;
----
=====

Now write a query to determine the other types that share the direct supertype of `city`.

.Hint
[%collapsible]
=====
The query should retrieve the subtypes of `place`.
=====

.Sample solution
[%collapsible]
=====
[,typeql]
----
match
$place-type sub place;
fetch
$place-type;
----
=====

Finally, write a query to retrieve all entity types in the schema.

.Hint
[%collapsible]
=====
The query should retrieve the subtypes of the root type `entity`.
=====

.Sample solution
[%collapsible]
=====
[,typeql]
----
match
$entity-type sub entity;
fetch
$entity-type;
----
=====

====

== Querying ownership interfaces

In addition to querying the type hierarchies defined in the schema, we can also query the defined *ownership* and *role* interfaces. In the following query, we retrieve the list of attribute types that the entity type `promotion` owns.

[,typeql]
----
match
promotion owns $attribute;
fetch
$attribute;
----

[,json]
----
result placeholder
----

To query ownerships, we use the `owns` keyword. As with the `sub` keyword, we can invert the intent of the statement by variablizing the object instead of the subject. We do this in the next query to retrieve the list of types that own the attribute type `isbn-13`.

[,typeql]
----
match
$type owns isbn-13;
fetch
$type;
----

[,json]
----
result placeholder
----

.Exercise
[caption=""]
====
Write a query to retrieve the list of types that own `name`.

.Sample solution
[%collapsible]
=====
[,typeql]
----
match
$type owns name;
fetch
$type;
----
=====

====

== Querying role interfaces

Ownerships of attribute types are queried using a single keyword `owns`, but roles in relations must be queried using two different keywords: `relates` and `plays`. We will see how they work in the next few examples. To begin with, we will use the `relates` keyword to retrieve the list of roles in the `locating` relation.

[,typeql]
----
match
locating relates $role;
fetch
$role;
----

[,json]
----
result placeholder
----

As we saw previously, role labels are denoted using both the name of the parent relation and the scoped name of the role. To query for types that play a role using the `plays` keyword, we must use the same notation.

[,typeql]
----
match
$type plays locating:located;
fetch
$type;
----

[,json]
----
result placeholder
----

Here we have queried the types that play the `located` role in `locating`. As with the `sub` and `owns` keywords, we can also invert the intent of `plays` [#_footnote_1]#statements#^<<_footnotes,1>>^. In the next query, we do so in order to retrieve the list of roles that `book` plays.

[,typeql]
----
match
book plays $role;
fetch
$role;
----

[,json]
----
result placeholder
----


.Exercise
[caption=""]
====
Write a query to retrieve the list of roles in the `delivery` relation.

.Sample solution
[%collapsible]
=====
[,typeql]
----
match
delivery relates $role;
fetch
$role;
----
=====

Now modify the query to also retrieve the types that play those roles.

.Sample solution
[%collapsible]
=====
[,typeql]
----
match
delivery relates $role;
$type plays $role;
fetch
$role;
$type;
----
=====

====

[#_footnotes]
== Footnotes

1. <<_footnote_1,^>> The astute reader will notice that we have used this "inversion" technique for `sub`, `owns`, and `plays` statements, but not for `relates` statements. TypeQL is flexible enough that we could certainly do so if we wanted, but it is not particularly useful to and there are certain practical considerations that make the syntax tricky to use correctly. While learning the basics of TypeQL, it is best to avoid `relates` statements of this kind.
