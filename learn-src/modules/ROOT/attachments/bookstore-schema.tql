#
# Copyright (C) 2022 Vaticle
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Affero General Public License as
# published by the Free Software Foundation, either version 3 of the
# License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Affero General Public License for more details.
#
# You should have received a copy of the GNU Affero General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.
#

define

book sub entity, abstract,
    owns title,
    owns page-count,
    owns isbn-10,
    owns isbn-13,
    plays contribution:work,
    plays publishing:published,
    plays order:product,
    plays rating:rated;

hardback sub book;

paperback sub book;

ebook sub book;

person sub entity,
    owns name,
    plays contribution:contributor,
    plays authorship:author,
    plays editing:editor,
    plays illustration:illustrator;

contribution sub relation,
    relates work,
    relates contributor;

authorship sub contribution,
    relates author as contributor;

editing sub contribution,
    relates editor as contributor;

illustration sub contribution,
    relates illustrator as contributor;

publisher sub entity,
    owns name,
    plays publishing:publisher;

publishing sub relation,
    relates published,
    relates publisher,
    owns year,
    plays location:located;

user sub entity,
    owns id,
    owns age,
    plays order:buyer,
    plays rating:rater,
    plays location:located,
    plays login:subject;

order sub relation,
    relates buyer,
    relates product,
    owns id,
    owns timestamp,
    owns status;

rating sub relation,
    relates rater,
    relates rated,
    owns score;

login sub relation,
    relates subject,
    owns timestamp,
    owns success;

place sub entity, abstract,
    owns name,
    plays location:place,
    plays location:located;

city sub place;

state sub place;

location sub relation,
    relates place,
    relates located;

title sub attribute, value string;
page-count sub attribute, value long;
isbn sub attribute, abstract, value string;
isbn-10 sub isbn;
isbn-13 sub isbn;
name sub attribute, value string;
year sub attribute, value long;
id sub attribute, value long;
age sub attribute, value long;
timestamp sub attribute, value datetime;
status sub attribute, value string, regex "^(paid|dispatched|delivered|returned|canceled)$";
score sub attribute, value long;
success sub attribute, value boolean;
